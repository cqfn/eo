+package org.eolang
+alias stdout org.eolang.io.stdout
+alias sprintf org.eolang.txt.sprintf
+junit

[] > forksOnCondition
  eq. > @
    42
    if.
      5.eq 8
      123
      42

[] > iteratesOverSimpleCounter
  memory > x
  and. > @
    x.write 5
    eq.
      4
      while.
        x.less 10
        [i]
          ^.x.write (^.x.add i) > @

[] > printsNiceFormulas
  memory > x
  seq > @
    x.write 2
    while.
      x.less 6
      [i]
        seq > @
          stdout
            sprintf "%dx%d = %d\n" (^.x) (^.x) (^.x.pow 2)
          ^.x.write (^.x.add 1)

# tests that bool.and stops calculations if its i'th
# object is false (including the base object)
[] > andShortCircuiting
  memory > mFirst
  memory > mThird
  and. > @
    not.
      and.
        (mFirst.write 1).not
        mFirst.write 2
        mFirst.write 3
    not.
      and.
        mThird.write 1
        mThird.write 2
        (mThird.write 3).not
        (mThird.write 4)
    mFirst.eq 1
    mThird.eq 3

# tests that bool.or stops calculations if its i'th
# object is true (including the base object)
[] > orShortCircuiting
  memory > mFirst
  memory > mThird
  and. > @
    or.
      mFirst.write 1
      mFirst.write 2
      mFirst.write 3
    or.
      (mThird.write 1).not
      (mThird.write 2).not
      mThird.write 3
      mThird.write 4
    mFirst.eq 1
    mThird.eq 3
